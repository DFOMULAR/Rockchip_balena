DESCRIPTION = "Linux kernel for Rock-4"

inherit kernel
inherit python3native
require recipes-kernel/linux/linux-yocto.inc

DEPENDS += "openssl-native"

SRC_URI = " \
	git://github.com/radxa/kernel.git;branch=linux-5.10-gen-rkr4.1 \
	file://brcmfmac.scc \
"

S = "${WORKDIR}/git"
SRCREV = "8f95261a006489c828f1d909355669875649668b"
SRC_URI[sha256sum] = "ac19848105e20e6a41afde48e694bfd6618da27c6b421f2cbb64985638222938"

LINUX_VERSION = "5.10.66-20-rockchip"
KCONFIG_MODE = "alldefconfig"

# Override local version in order to use the one generated by linux build system
# And not "yocto-standard"
LINUX_VERSION_EXTENSION = ""
PR = "r1"
PV = "${LINUX_VERSION}"

# Include only supported boards for now
COMPATIBLE_MACHINE = "(rk3036|rk3066|rk3288|rk3328|rk3399|rk3308)"

do_compile_append() {
	oe_runmake dtbs
}

# Make sure we use /usr/bin/env ${PYTHON_PN} for scripts
do_patch_append() {
	for s in `grep -rIl python ${S}/scripts`; do
		sed -i -e '1s|^#!.*python[23]*|#!/usr/bin/env ${PYTHON_PN}|' $s
	done
}

do_deploy_append() {
	install -d ${DEPLOYDIR}/overlays
	install -m 644 ${WORKDIR}/linux-rock_4*/arch/arm64/boot/dts/rockchip/overlays-rock4/* ${DEPLOYDIR}/overlays
	install -m 644 ${S}/arch/arm64/boot/dts/rockchip/overlays-rock4/hw_intfc.conf ${DEPLOYDIR}/
}


